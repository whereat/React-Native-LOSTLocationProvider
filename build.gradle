buildscript {
    repositories {
        jcenter()
    }


    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = "io.whereat.lib"
version = "0.0.1"

repositories {
    jcenter()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.facebook.react:react-native:0.20.0'
    compile 'com.mapzen.android:lost:1.0.1'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'junit:junit:4.12'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId 'io.whereat.lib'
                artifactId 'lostlocationprovider'

                name "React-Native-LOSTLocationProvider"
                description "LOST Location Provider React Native library"

                licenses {
                    license {
                        name 'GPL v3.0'
                        url 'http://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }

                scm {
                    connection 'https://github.com/whereat/React-Native-LOSTLocationProvider.git'
                    developerConnection 'https://github.com/whereat/React-Native-LOSTLocationProvider.git'
                    url 'https://github.com/whereat/React-Native-LOSTLocationProvider'
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "LOSTLocationProvider"
        websiteUrl = 'https://github.com/whereat/React-Native-LOSTLocationProvider'
        vcsUrl = 'https://github.com/whereat/React-Native-LOSTLocationProvider.git'
        licenses = ["GPL-3.0"]
        publish = true
    }
}